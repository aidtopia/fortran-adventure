@ECHO OFF
SETLOCAL
SET TESTRESULT=1
SET STARTINGDIR=%CD%
SET SRCDIR=%1
IF "%SRCDIR%"=="" SET SRCDIR=%~dp0\..\WOOD0350v2\
CD %SRCDIR%
ECHO:
ECHO Translating Adventure from Fortran to C...
FOR %%f IN (adv*.f*) DO %~dp0..\fortran\x64\Debug\fortran %%f
SET TESTRESULT=%ERRORLEVEL%
IF %TESTRESULT% NEQ 0 GOTO DONE
ECHO:
ECHO Compiling the C code...
CD target
SET OPTOPT="/Od"
REM SET OPTOPT="/O2 /DNDEBUG /GF"
REM TODO:  Add /WX to the compiler options.
cl /nologo /std:c11 /W4 %OPTOPT% /ZI ADV*.c
SET TESTRESULT=%ERRORLEVEL%
CD ..
IF %TESTRESULT% NEQ 0 GOTO DONE
ECHO:
ECHO Running Adventure...
ECHO:
DIR /B /S target\adv*.exe > target\run.bat
SET TESTRESULT=%ERRORLEVEL%
IF %TESTRESULT% NEQ 0 GOTO DONE
CALL target\run.bat
SET TESTRESULT=%ERRORLEVEL%
ECHO:
ECHO Adventure completed.
:DONE
CD /D %STARTINGDIR%
ECHO:
IF %TESTRESULT% NEQ 0 ECHO Failed (exit code was %TESTRESULT%)
IF %TESTRESULT% EQU 0 ECHO SUCCESS!
ECHO ON
@EXIT /B %TESTRESULT%
